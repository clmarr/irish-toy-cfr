$Proto-Irish to Old Irish cascade!

$although this language treats nasal stops as proper stops and [-cont] that's a change to symbolDefs I'm too lazy to implement at the moment, so we are instead handling that distinction with length. Implementing long variant default. Implement the same for l and r since it seems to work the same. 
[+son,+cons] > [+long]

$syllable initial w develarizes and also devoices to f for now (pending later developments.)
w > f / __ [+syl]

$o effaced in hiatus (per handout I believe)
o > ∅ / __ [+syl]

$innovation of word initial stress. Taking into account that word boundaries that are going to be removed before long are currently represented by ʔ
[+syl] > [+prim] / {#;ʔ} ([-syl])* __

$deletion of the word-boundary in nominal and verbal complexes it seems. This appears to happen after the first lenition, but before the first final consonant lost. Recall that this evanescent word boundary is being represented by <ʔ> 
ʔ > ∅

$First Lenition: stops spirantize and sonorants "shorten" intervocalically. After deletion of word-boundary in these complexes. 
[-cont] > [+cont] / [-cons,+son] __ [+syl] 

$per thurneysen paragraph 120 -- lenition of sonorants in clusters not involving dentals (incl. dental sonorants). By dental I assume he means coronal. 
[+son,+cons,-cor] > [-long] / [-cons,+son,-cor] __ [+syl]
[+son,+cons] > [-long] / [+syl] __ [+syl]

$First lenition part two: s to χ (later secondary h) and ɸ to h post-vocalically 
{s;f} > {χ;h} / [-cons,+son] __ 

$loss of post-non-consonantal final consonants. This must come after the word boundary deletion with esan, given the nasalized onset form agenel. 
[+cons] > ∅ / [-cons] __ #

$e lowered before x. Fed by aspirating lenition.
[+front,-hi] > [+lo,+back,-front] / __ [+back,+cont]

$consonants before i, e patalalize. This must happen before apocope (ri:mi:ti > r:ʲiṽʲiθʲ)
[+cons,-front] > [+hi,+front] / __ [+front]

$o raises to u if next syllable has high vowel (as noted in write up). This must be before apocope (goni, mrogi). Must also be before syncope (moniklas)
	$ this would be alone, [+round,+syl] > [+hi] / __ ([-syl])* [+syl,+hi]
$it seems e and also rises before i. Must be before syncope (wenikaros, esosu:lis), relationship to apocope unclear. 
	$ these two together would make the following rule
[-lo,+syl] > [+hi] / __ ([-syl])* [+syl,+hi]

$ furthermore, and i lowers to e if next syllable has 'low vowel', like a, per the writeup. Upon further investigation this *also* happens if next syllable has o (tori:mo, bito:s). However this does not bleed the previous rule by changing i to e before it can act (wenikaros > finchar, moniklas > mune:l) ... the other explanation would be that it only effects stressed elements, but this hypothesis fails for moniklas.
$ moniklas also shows that this lowering rule only operates once, and does not cascade (ohterwise it would be mone:l not mune:l). 
$ this can be roped in with apparent similar effects on u: klutan > kloθ, katubutas > kathvath. 
$ [-hi,+back] captures a and o to the exclusion of e. 
[+hi,+syl] > [-hi] / __ ([-syl])* [-hi,+back,+syl] 

$countertonic (secondary) stress imposed on odd numbered syllables unless they are word final. This is done by detecting the intermediate unstressed syllable and then jumping over it. Must come before syncope and before compensatory lengthening for nasal and stop deletions in clusters. Does not apply to word-final vowels (post-context: @, to be blocked before #). Applies recursively. Two times application is probably enough for words in this set, they don't get that long. 
[+syl] > [+stres] / [+stres] ([-syl])* [-stres] ([-syl])* __ @
[+syl] > [+stres] / [+stres] ([-syl])* [-stres] ([-syl])* __ @

$countertonic stress imposed on heavy penultimates before light ultimates
[+syl,-stres] > [+stres] / __ [-syl] ([-syl])+ [+syl] #

=Stressed-Irish

$unstressed penultimates that are not before stressed final syllables acquire secondary stress if before (at least?) two vowels. 
[+syl] > [+stres] / __ [-syl] ([-syl])+ [-stres] ([-syl])* #

$ apparent o to a after labial (reassignment of rounding/height gesture?) 
[-hi,+round,+syl] > [+lo,-round,-lab] / [+lab] __ 

$nasals totally assimilate to following n m l r s, forming geminates. No lengthening caused in this case. 
[+nas,+cons] [+son,+cons] > ∅ [+long]
[+nas,+cons] s > ∅ [+long]


$Second lenition: voicing spreads progressively for stops in consonant clusters; stops are also voiced after vowels. Either bled by spirantization in first lenition and/or counterfed by syncope (finchar). Only voiceless obstruent affected is f after nasals (which has not yet merged with ɸ < p)
[-son,-cont] > [+voi] / [+voi] __ {#;[+voi]}
f > v / [+nas,+voi] __

$nasal consonant deletion in V __ C : must come AFTER POST-SONORANT STOP VOICING (tent > te:d), but BEFORE SYNCOPE (tomthed). 
$compensatory lengthening before nasal deletion :
$ would be:  [+stres] > [+long] / __ [+nas,+cons] [-son] 
	$ followed by [+nas,+cons] > ∅ / [-cons] __ [+cons,-nas]
$seems limited to primary or secondary stressed elements.
$ the same thing appears to happen with stops as the first elements in clusters
$ another relevant element is that these elements do not seem to be effaced word initialy: mruig, cloth, etc. 
$ but the language no longer respects word boundaries so the real way these are distinguished seems to be ... being in the onset of the tonic syllable.
$ handle this with a dummy h, the result of s > χ, which is about to disappear in these positions
$ this all would be much more natural, with a single rule even, if symbolDefs had the nasals as noncontinuant... 
	$ which may be how Old Irish was treating them! 
[+nas] > h / __ [-syl] ([-syl])* {[+syl,-prim];#}
[-cont] > h / __ [-syl] ([-syl])* {[+syl,-prim];#}
[+stres] > [+long] / __ h


$APOCOPE and SYNCOPE handled together here, in terms of stress

$pseudo-apocope and -syncope: unstressed short vowels are lost, while long vowels lose a mora. (in effect short vowels lose their only mora. 
[-stres,-long] > ∅
[-stres] > [-long]

$ reflex of s passes to h, and disappears in any consonant cluster it is not the last element of. Before cluster palatality assimilation.
χ > h 
h > ∅ / __ [-syl]

$cluster palatality assimilation (regressive) 
[+back,+cons] > [-front] / __ [-front]
[-back,+cons] > [-front,-hi] / __ [-front] 
[+cons] > [+front,+hi] / __ [+front,+cons]

$n-place assimilates to following stops
$this should be done with alpha features but alas.
{n;nː} > {ŋ;ŋː} / __ [-cont,+back]
{n;nː} > {m;mː} / __ [-cont,+lab]


$orthographic stress removed (place this late)
[+stres] > [-stres]